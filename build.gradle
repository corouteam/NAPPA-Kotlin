plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.6.5'
    id "org.sonarqube" version "2.8"
    id "org.jetbrains.kotlin.jvm" version "1.4.10"

}

apply plugin: "kotlin"
apply plugin: "org.jetbrains.intellij"

group 'nl.vu.cs.s2group.nappa.plugin'
version '1.1.5'

sourceCompatibility = 1.8

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

// When this option is enabled and `ideDirectory` is set to Android Studio,
// the build fails and generates the exception https://youtrack.jetbrains.com/issue/KT-32401
// As a result of disabling building searchable options,
// the configurables that your plugin provides won't be searchable in the Settings dialog.
buildSearchableOptions.enabled = false


repositories {
    mavenCentral()
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.4.10")
    // implementation("org.jetbrains.kotlin:kotlin-compiler-embeddable:1.3.21")
}

intellij {
    // Refer to the docs for more information
    // https://github.com/JetBrains/gradle-intellij-plugin
    // https://www.jetbrains.org/intellij/sdk/docs/products/android_studio.html
    // Needs to be defined in the Gradle installation directory
    version project.hasProperty('nappaAndroidStudioVersion') ? nappaAndroidStudioVersion : 'LATEST-EAP-SNAPSHOT'
    plugins = ['com.intellij.java', 'org.jetbrains.kotlin']

    updateSinceUntilBuild false
}

if (project.hasProperty('nappaAndroidStudioHome')) {
    runIde {
        // Needs to be defined in the Gradle installation directory
        ideDirectory nappaAndroidStudioHome
    }
}

patchPluginXml {
    changeNotes
}

sonarqube {
    properties {
        property "sonar.projectKey", nappaSonarOrganization + "_nappa-plugin-android-studio"
        property "sonar.projectName", "NAPPA - Plugin for Android Studio"
        property "sonar.organization", nappaSonarOrganization
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.login", nappaSonarLogin
    }
}
